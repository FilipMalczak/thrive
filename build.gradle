//todo exploit gradle caching https://circleci.com/docs/2.0/language-java/
buildscript {
    ext {
        //fixme these should be somehow resolvable against BOM
        springBootVersion  = '2.1.6.RELEASE'

        thriveBomVersion   = "0.2.0-SNAPSHOT"

        gitInfoVersion     = '2.0.0'
    }
    repositories {
        mavenCentral()
        maven {
            name "jitpack"
            url "https://jitpack.io"
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.thrive-framework:thrive-service-plugin:0.3.0-SNAPSHOT"
        classpath "com.github.thrive-framework:thrive-versioning-plugin:0.1.0"
        classpath "com.github.thrive-framework:thrive-package-plugin:0.1.0-SNAPSHOT"
    }
}

ext {
    containerizationGroup = "thrive (docker)"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.thrive-versioning"

    group = 'com.github.thrive-framework'

    //todo this is tricky - without it, artifacts may not be built before install;
    // on the other hand, should this be a default when using versioning plugin (because that's where this
    // belongs)? at some point (probably when extracting plugin-commons), do this cleanup
    install.dependsOn tasks.build
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: "com.github.thrive"

    thrive {
        libraries {
            thriveCommon false
        }
        dockerfile {
            maintainer = "Filip Malczak (filip.malczak@gmail.com)"
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

def dockerizedSubprojects() {
    subprojects
        .findAll {
            try {
                it.dockerized
            } catch (MissingPropertyException e){
                false
            }
        }
}

//fixme it will work as long as no-one will change project name; project names need to be the same as dir names and only 1-level hierarchy is allowed for now
task listDockerizedProjectsDirectories(
    group: containerizationGroup,
    description: "Writes dockerized subproject names to <root project build dir>/dockerizedProjects.txt, one per line"
) {
    doFirst {
        project.buildDir.mkdirs()
        new File(project.buildDir, "dockerizedProjects.txt").text = dockerizedSubprojects()
            .collect {
                println "Found dockerized project ${it.name}"
                it.name
            }.join "\n"
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.5'
}