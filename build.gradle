import java.time.LocalDateTime
import java.time.ZoneId

buildscript {
    ext {
        springCloudReleaseTrain = 'Greenwich.RELEASE' // relates to Spring Boot 2.1.x
        springDataReleaseTrain = 'Lovelace-RELEASE'
        springBootVersion  = '2.1.3.RELEASE'
        springBootAdminVersion = "2.1.3"
        gitInfoVersion     = '2.0.0'
        lombokVersion      = "2.1"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gitInfoVersion}"
        classpath 'com.google.guava:guava:20.0'

    }
}

ext {
    containerizationGroup = "containerization"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.gorylenko.gradle-git-properties"

    group = 'com.github.filipmalczak'

    version = "0.2.0-SNAPSHOT"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    //todo normalize dependency notation "group:project[:version" against group: group, name: project[, version: version]
    //I think that single-string is better

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "io.r2dbc:r2dbc-bom:Arabba-M8" //fixme version -> ext
//            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudReleaseTrain}"
//            mavenBom "org.springframework.data:spring-data-releasetrain:${springDataReleaseTrain}"
        }
        dependencies {
            dependency group: 'de.codecentric', name: "spring-boot-admin-starter-client", version: springBootAdminVersion
            dependency group: 'de.codecentric', name: "spring-boot-admin-starter-server", version: springBootAdminVersion

            dependency group: 'ma.glasnost.orika', name: 'orika-core', version: '1.5.2'

            dependency 'org.junit.jupiter:junit-jupiter-api:5.3.1'
            dependency group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.0'
            dependency 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

            dependency group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
            dependency 'org.junit.vintage:junit-vintage-engine:5.3.1' //required for Spock
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'

            dependency "com.github.sbrannen:spring-test-junit5:1.2.0"

            dependency 'org.projectlombok:lombok:1.18.2'
            
            dependency 'com.kstruct:gethostname4j:0.0.2'

            dependency 'io.springfox:springfox-swagger2:3.0.0-SNAPSHOT'
            dependency 'io.springfox:springfox-swagger-ui:3.0.0-SNAPSHOT'
            dependency 'io.springfox:springfox-spring-webflux:3.0.0-SNAPSHOT'

            dependency "io.projectreactor.kafka:reactor-kafka:1.1.0.RELEASE"

            dependency "org.springframework.cloud:spring-cloud-starter-gateway:2.1.1.RELEASE"
            dependency "org.apache.zookeeper:zookeeper:3.4.12"

            dependency('org.springframework.cloud:spring-cloud-starter-zookeeper-all:2.1.1.RELEASE')
            dependency group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6'
            dependency group: 'org.springframework.boot.experimental', name: 'spring-boot-starter-data-r2dbc', version: '0.1.0.M1'
            dependency group: 'org.springframework.boot.experimental', name: 'spring-boot-actuator-autoconfigure-r2dbc', version: '0.1.0.M1'
            dependency group: 'io.r2dbc', name: 'r2dbc-postgresql', version: '0.8.0.M8'
        }
    }

    dependencies {
        compileOnly('org.projectlombok:lombok') //fixme
        annotationProcessor('org.projectlombok:lombok')
    }

    pluginManager.withPlugin("org.springframework.boot") {
        ext {
            dockerized = true
        }

        project.springBoot {
            buildInfo {
                //todo it would be nice to inject git and build data to manifest as well
                def tz = ZoneId.systemDefault();
                def now = LocalDateTime.now().atZone(tz)
                properties {
                    time = now.toInstant()
                    additional = [
                        timezone: tz.toString(),
                        timestamp: now.toEpochSecond()
                    ]
                }
            }
        }

        project.bootRun {
            systemProperty "spring.profiles.active", "local"
        }

        project.bootJar {
            classifier "boot"
        }

        //todo clean should remove this files, same with dockerized project list
        task generateDockerfile(
            group: containerizationGroup,
            description: "Creates a Dockerfile in project directory"
        ) {
            doFirst {
                new File(project.projectDir, "Dockerfile").text = """
FROM openjdk:8-jre-alpine

LABEL maintainer="Filip Malczak (filip.malczak@gmail.com)"

WORKDIR /var/opt/${project.name}

COPY ./build/libs/${project.name}-${project.version}-boot.jar ./${project.name}-${project.version}-boot.jar

ENTRYPOINT ["java","-jar","/var/opt/${project.name}/${project.name}-${project.version}-boot.jar"] 
""" //todo: JAVA_OPTS
            }
        }
    }

    task writeVersion(
        group: containerizationGroup,
        description: "Writes project version to <subproject build dir>/generated/meta/version.txt"
    ) {
        doFirst {
            def metaDir = new File(project.buildDir, "generated/meta")
            metaDir.mkdirs()
            new File(metaDir, "version.txt").text = project.version
        }
    }
}

def dockerizedSubprojects() {
    subprojects
        .findAll {
            try {
                it.dockerized
            } catch (MissingPropertyException e){
                false
            }
        }
}

//fixme it will work as long as no-one will change project name; project names need to be the same as dir names and only 1-level hierarchy is allowed for now
task listDockerizedProjectsDirectories(
    group: containerizationGroup,
    description: "Writes dockerized subproject names to <root project build dir>/dockerizedProjects.txt, one per line"
) {
    doFirst {
        project.buildDir.mkdirs()
        new File(project.buildDir, "dockerizedProjects.txt").text = dockerizedSubprojects()
            .collect {
                println "Found dockerized project ${it.name}"
                it.name
            }.join "\n"
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.2.1'
}